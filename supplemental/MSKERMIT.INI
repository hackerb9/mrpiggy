;; Tip: if screen won't stop scrolling with tons of data,
;; you can use the `clear dev` command to discard the device buffer.


;; Double check that this file will work right.
if > 0 1-
  echo {Dash continuation lines aren't working in MSKERMIT.INI.},-
  echo {This happens when lines end with \n instead of \r\n.},-
  echo {Kermit can mess this up when downloading the file.},-
  echo {To fix, run this before download: set trasfer mode automatic},-
  end

echo Loading mskermit.ini

;; Disable printer so kermit doesn't freeze up when I hit Shift+PrtSc
;; Nope. The problem is in DOS. Kermit never see the key!
set printer nul

; Macros for communicating with UNIX, VAX/VMS, and similar systems:
;
def text set file type text
def binary set file type binary

def _bs check term, if success set key \270 \%1

def 8bit set parity none, set term bytesize 8, set term controls 8
def 7bit set term bytesize 7, set term controls 7
def vax set par non, set hand no, set local-echo off, _bs \127
def vms set term type vt320, do vax, do 8bit
def unix do vax, do 7bit
def bbs set term type ansi, do vax, do 8bit

; Macro for communicating with IBM mainframes in linemode:
;
def ibm set par mark, set flo no, set hand xon, set dup ha, _bs \8

; Macro for communicating with IBM mainframes in fullscreen mode,
; through 3270 protocol converter like IBM 7171:
;
def FULLSCREEN set par ev,set flo x,set hand no,set dupl ful, _bs \8

; Additional DOS commands available directly from the MS-Kermit> prompt.

; DOS MORE command... (no "<" needed)
def more if < argc 2 fatal {More what?},-
  run more < \%1

; DOS REN(ame) command...
def rename if < argc 2 fatal {Rename what?},-
  if < argc 3 fatal {Rename \%1 to what?},-
  run ren \%1 \%2

; DOS COPY command...
def copy if < argc 2 fatal {Copy what?},-
  if < argc 3 fatal {Copy \%1 to what?},-
  run copy \%1 \%2

; DOS CHCP command to examine or change code page...
def chcp run chcp \%1

; UNIX-like commands
def ls if < argc 2 run dir /w,-
       if equal \%1  -l run dir
def pwd echo \v(dir)
def rm del \%1
def fg connect



; STATUS macro tells whether previous command succeeded or failed,
; preserving its return code.
define STATUS asg \%9 \v(status),-
 if = \%9 0 echo SUCCESS,-
 if not = \%9 0 echo FAILURE,-
 end \%9

; Define a FATAL error macro.  Print message, go back to prompt.
def fatal stop 1 \%1

; ERRSTP macro, for use in scripts that want to hang up a dialed
; or network connection if there is any error.
def errstp echo \%1,hangup,stop 1

:XFER

COMMENT - File transfer settings and macros

set transfer mode automatic	; Make sure line endings aren't messed up.
;set file type binary		; Default to not translating text.
set file collision rename	; Don't overwrite existing files
set transfer character-set transparent ; No surprise translations
set attributes on		; Use file-attribute packets

; FAST kermit data transfer
; assumes a clean, well-buffered, effectively-flow-controlled connection.
set window 5			; Use 5 sliding windows
set receive packet-length 9024	       ; Long packets
set block-check-type 3-char-CRC-CCITT  ; Use 3-byte CRC to detect errors
set control-character unprefixed all   ; Quote a minimum of control chars
set control-character prefixed 0 1 129 ; See: columbia.edu/kermit/perf.html

COMMENT - Script defaults

set input timeout proceed	; Allow IF SUCCESS/FAILURE to work
set input echo on		; Display port input on screen
set input case ignore		; Ignore alphabet case for input matching

; The following setups are IBM-PC specific...

set terminal display 8		; In the 7-bit environment (use 8 for VMS)
set terminal vt320		; Emulate DEC VT320
set terminal roll off		; Keep rolled-back screens where they are
set terminal wrap on		; Tell Kermit to do line wrap
set terminal tabs at 1:8	; Set tabs at every 8 spaces
;set terminal cursor underline	; Underline cursor (rather than block)

;set terminal color 0 37 44	; gray on blue (normal)
;set terminal underscore 1 37 42	; white on green (extra bright)
				; (looks reasonable on amber-CGA)

;;set terminal underscore 1 37 47	; white on gray
;; Cannot use white on gray!
;; Note that after 'man 5 terminfo', Kermit stops using the extra
;; bright flag on underscored characters. Why? Buggy most likely.
;; Could be bad terminfo for vt320?

;; XXX kermit bug: no way to modify other attributes
;; Currently, kermit-315 has these hardcoded in:
;; alternate character set	ACS WORKS CORRECTLY (hurray!)
;; bold				BOLD (extra bright)
;; blink			BLINKS (umm... hurray?)
;; reverse			reverse video colors
;; standout			reverse video colors
;; (dim)			not supported?
;; (italics)			not supported?




check tcp
if fail forward CUSTOM

echo Setting up TELNET over TCP/IP

; TELNET macro for making TCP/IP connections using Kermit's built-in TCP/IP.
; "TELNET <ip-host-name-or-address>" makes a new TCP/IP connection.
; "TELNET" (by itelf) resumes the current TCP/IP connection.
;   \%1 = IP host name or address (optional)
;   \%2 = TCP port (optional)
;   \%3 = terminal type (optional)
;
define TELNET set flow none, set port tcp \%1 \%2, pause 0, if fail end 1, if def \%3 set term type \%3, if success connect

SET TCP/IP ADDRESS 10.5.5.10
SET TCP/IP PRIMARY-NAMESERVER 8.8.8.8
SET TCP/IP SECONDARY-NAMESERVER 1.1.1.1
SET TCP/IP GATEWAY 10.5.5.5
;; Default host to connect to.
;;SET PORT TCP/IP NETHACK.ALT.ORG
SET PORT TCP/IP 10.5.5.5
SET DISPLAY 8-BIT

;; Alt+F1 to F6 select a TCP/IP session
set key \2408 \Ksession1
set key \2409 \Ksession2
set key \2400 \Ksession3
set key \2401 \Ksession4
set key \2402 \Ksession5
set key \2403 \Ksession6

;; Alt+F9 to switch to next session
set key \2416 \KnextSession

define NEWSESSION set flow none, ask \%h Telnet to?, set port tcp \%h new, pause 0, if fail end 1, if success connect

;; Alt+F10 to open a new telnet session
set key \2417 \KNEWSESSION




COMMENT - Personalized initialization file, if any

:CUSTOM
if inpath MSCUSTOM.INI take MSCUSTOM.INI

;; Use Alt as Meta for Emacs, readline
if inpath EMACS.INI take EMACS.INI

;; Shift+Esc is tilde
set key \769 ~

;; I don't have a printer, so don't lock up Kermit when I hit printscreen.
set printer nul

;; Alias so I can quickly reset colors when they become unreadable on
;; my IBM 5155 amber screen.
define 0 set term color 0

;; xxx todo: bind PrtSc to 'clear device-buffer'
;; XXX todo: bind Alt-Tab to "next tcp/ip session"
;;     XXX Kermit can't detect alt-tab. :-(
;; xxx todo: autoexec.bat
;; Xxx todo: disable printscreen from locking up kermit


echo Smile!
